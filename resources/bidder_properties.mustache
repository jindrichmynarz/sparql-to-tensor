{{!
@param limit   int
@param offset  int
}}

PREFIX ares: <http://linked.opendata.cz/ontology/ares#>
PREFIX pc:   <http://purl.org/procurement/public-contracts#>
PREFIX rov:  <http://www.w3.org/ns/regorg#>

SELECT ?feature ?s ?o
WHERE {
  {
    SELECT ?s
    WHERE {
      {
        SELECT DISTINCT ?s
        WHERE {
          GRAPH <http://linked.opendata.cz/resource/dataset/isvz.cz> {
            [] pc:awardedTender/pc:bidder ?s .
          }
        }
        ORDER BY ?s
      }
    }
    LIMIT {{limit}}
    OFFSET {{offset}}
  }
  VALUES (?graph                                                ?feature) {
         (<http://linked.opendata.cz/resource/dataset/ares/res> rov:orgActivity)
         (<http://linked.opendata.cz/resource/dataset/ares/rzp> ares:zivnost)
  }
  GRAPH ?graph {
    ?s ?feature ?o .
  }
}
