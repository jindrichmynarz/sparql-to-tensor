{{!
@param limit   int
@param offset  int
}}

PREFIX isvz: <http://linked.opendata.cz/ontology/isvz.cz/>
PREFIX pc:   <http://purl.org/procurement/public-contracts#>

SELECT ?s ?o ?feature
FROM <http://linked.opendata.cz/resource/dataset/isvz.cz>
WHERE {
  {
    SELECT ?s
    WHERE {
      {
        SELECT DISTINCT ?s
        WHERE {
          ?s a pc:Contract ;
            pc:lot/pc:awardedTender/pc:bidder [] .
        }
        ORDER BY ?s
      }
    }
    LIMIT {{limit}}
    OFFSET {{offset}}
  }
  VALUES ?feature {
    isvz:serviceCategory
    pc:additionalObject
    pc:kind
    pc:mainObject
  }
  ?s ?feature ?o .
}
